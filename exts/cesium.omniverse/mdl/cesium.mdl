mdl 1.8;

import ::anno::*;
import ::state::*;
import ::tex::*;
import ::scene::*;
import ::math::*;

using ::gltf::pbr import *;

module [[
    anno::version( 1, 0, 0),
    anno::display_name("Cesium MDL functions")
]];

annotation annotation_not_connectable();

export const auto DEFAULT_NULL_FEATURE_ID = -1;
export const auto DEFAULT_PROPERTY_VALUE_BOOL = false;
export const auto DEFAULT_PROPERTY_VALUE_BOOL2 = bool2(false);
export const auto DEFAULT_PROPERTY_VALUE_BOOL3 = bool3(false);
export const auto DEFAULT_PROPERTY_VALUE_BOOL4 = bool4(false);
export const auto DEFAULT_PROPERTY_VALUE_INT = 0;
export const auto DEFAULT_PROPERTY_VALUE_INT2 = int2(0);
export const auto DEFAULT_PROPERTY_VALUE_INT3 = int3(0);
export const auto DEFAULT_PROPERTY_VALUE_INT4 = int4(0);
export const auto DEFAULT_PROPERTY_VALUE_FLOAT = 0.0;
export const auto DEFAULT_PROPERTY_VALUE_FLOAT2 = float2(0.0);
export const auto DEFAULT_PROPERTY_VALUE_FLOAT3 = float3(0.0);
export const auto DEFAULT_PROPERTY_VALUE_FLOAT4 = float4(0.0);
export const auto DEFAULT_PROPERTY_VALUE_FLOAT2x2 = float2x2(0.0);
export const auto DEFAULT_PROPERTY_VALUE_FLOAT3x3 = float3x3(0.0);
export const auto DEFAULT_PROPERTY_VALUE_FLOAT4x4 = float4x4(0.0);
const auto DEFAULT_OFFSET_FLOAT = 0.0;
const auto DEFAULT_OFFSET_FLOAT2 = float2(0.0);
const auto DEFAULT_OFFSET_FLOAT3 = float3(0.0);
const auto DEFAULT_OFFSET_FLOAT4 = float4(0.0);
const auto DEFAULT_SCALE_FLOAT = 1.0;
const auto DEFAULT_SCALE_FLOAT2 = float2(1.0);
const auto DEFAULT_SCALE_FLOAT3 = float3(1.0);
const auto DEFAULT_SCALE_FLOAT4 = float4(1.0);

// Should match MAX_IMAGERY_LAYERS_COUNT in FabricMaterial.cpp
const auto MAX_IMAGERY_LAYERS_COUNT = 16;

export enum up_axis_mode {
    Y,
    Z
};

float2 world_coordinate_2d(float2 min_world, float2 max_world, up_axis_mode up_axis)
{
    // Get the world pos of the pixel
    auto world_pos = state::transform_vector(state::coordinate_internal, state::coordinate_world, state::position());
    auto world_pos_horizontal = float2(world_pos.x, up_axis == Y ? world_pos.z : world_pos.y);

    // Return 0-1 UVs based on the min/max world coordinates provided
    return (world_pos_horizontal - min_world) / (max_world - min_world);
}

export float4 cesium_lookup_world_texture_float4(
    uniform texture_2d texture = texture_2d(),
    float2 min_world = float2(-5000.0, -5000.0),
    float2 max_world = float2(5000.0, 5000.0),
    up_axis_mode up_axis = Y)
[[
    anno::display_name("Cesium world-mapped texture lookup float4"),
    anno::description("Returns float4 from a texture mapped to world UV coordinates"),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return tex::lookup_float4(
        tex: texture,
        coord: world_coordinate_2d(min_world, max_world, up_axis),
        wrap_u: ::tex::wrap_clamp,
        wrap_v: ::tex::wrap_clamp);
}

export float3 cesium_lookup_world_texture_float3(
    uniform texture_2d texture = texture_2d(),
    float2 min_world = float2(-5000.0, -5000.0),
    float2 max_world = float2(5000.0, 5000.0),
    up_axis_mode up_axis = Y)
[[
    anno::display_name("Cesium world-mapped texture lookup float3"),
    anno::description("Returns float3 from a texture mapped to world UV coordinates"),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return tex::lookup_float3(
        tex: texture,
        coord: world_coordinate_2d(min_world, max_world, up_axis),
        wrap_u: ::tex::wrap_clamp,
        wrap_v: ::tex::wrap_clamp);
}

export color cesium_lookup_world_texture_color(
    uniform texture_2d texture = texture_2d(),
    float2 min_world = float2(-5000.0, -5000.0),
    float2 max_world = float2(5000.0, 5000.0),
    up_axis_mode up_axis = Y)
[[
    anno::display_name("Cesium world-mapped texture lookup color"),
    anno::description("Returns color from a texture mapped to world UV coordinates"),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return tex::lookup_color(
        tex: texture,
        coord: world_coordinate_2d(min_world, max_world, up_axis),
        wrap_u: ::tex::wrap_clamp,
        wrap_v: ::tex::wrap_clamp);
}

export float4 cesium_base_color_texture_float4(
    gltf_texture_lookup_value base_color_texture = gltf_texture_lookup_value()
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]]
)
[[
    anno::display_name("Cesium base color texture lookup float4"),
    anno::description("Returns the base color texture as a float4. Returns [0, 0, 0, 0] if the base color texture does not exist."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return base_color_texture.valid ? base_color_texture.value : float4(0.0);
}

export float4 cesium_imagery_layer_float4(
    gltf_texture_lookup_value imagery_layer = gltf_texture_lookup_value()
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform int imagery_layer_index = 0
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium imagery layer lookup float4"),
    anno::description("Returns the imagery layer at the given index as a float4. Returns [0, 0, 0, 0] if the imagery layer does not exist."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return imagery_layer.valid ? imagery_layer.value : float4(0.0);
}

export int cesium_feature_id_int(
    int feature_id = DEFAULT_NULL_FEATURE_ID
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform int feature_id_set_index = 0
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium feature ID lookup int"),
    anno::description("Returns the feature ID for the given feature ID set. Returns -1 if the feature ID set does not exist or the feature ID is equal to nullFeatureId in the EXT_mesh_features extension."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return feature_id;
}

export bool cesium_property_bool(
    bool property_value = DEFAULT_PROPERTY_VALUE_BOOL
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup bool"),
    anno::description("Returns the property value. Returns false if the property does not exist or is an incompatible type. Must be a scalar boolean type."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export bool2 cesium_property_bool2(
    bool2 property_value = DEFAULT_PROPERTY_VALUE_BOOL2
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup bool2"),
    anno::description("Returns the property value. Returns bool2(false) if the property does not exist or is an incompatible type. Must be a 2-component boolean type."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export bool3 cesium_property_bool3(
    bool3 property_value = DEFAULT_PROPERTY_VALUE_BOOL3
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup bool3"),
    anno::description("Returns the property value. Returns bool3(false) if the property does not exist or is an incompatible type. Must be a 3-component boolean type."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export bool4 cesium_property_bool4(
    bool4 property_value = DEFAULT_PROPERTY_VALUE_BOOL4
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup bool4"),
    anno::description("Returns the property value. Returns bool4(false) if the property does not exist or is an incompatible type. Must be a 4-component boolean type."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export int cesium_property_int(
    int property_value = DEFAULT_PROPERTY_VALUE_INT
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup int"),
    anno::description("Returns the property value. Returns 0 if the property does not exist or is an incompatible type. Must be a scalar unnormalized integer or enum type. Values that exceed the 32-bit signed integer range are clamped.
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export int2 cesium_property_int2(
    int2 property_value = DEFAULT_PROPERTY_VALUE_INT2
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup int2"),
    anno::description("Returns the property value. Returns int2(0) if the property does not exist or is an incompatible type. Must be a 2-component unnormalized integer or enum type. Values that exceed the 32-bit signed integer range are clamped.
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export int3 cesium_property_int3(
    int3 property_value = DEFAULT_PROPERTY_VALUE_INT3
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup int3"),
    anno::description("Returns the property value. Returns int3(0) if the property does not exist or is an incompatible type. Must be a 3-component unnormalized integer or enum type. Values that exceed the 32-bit signed integer range are clamped.
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export int4 cesium_property_int4(
    int4 property_value = DEFAULT_PROPERTY_VALUE_INT4
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup int4"),
    anno::description("Returns the property value. Returns int4(0) if the property does not exist or is an incompatible type. Must be a 4-component unnormalized integer or enum type. Values that exceed the 32-bit signed integer range are clamped.
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export float cesium_property_float(
    float property_value = DEFAULT_PROPERTY_VALUE_FLOAT
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup float"),
    anno::description("Returns the property value. Returns 0.0 if the property does not exist or is an incompatible type. Must be a scalar floating point or normalized integer type. 64-bit floating point values are converted to 32-bit floating point values."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export float2 cesium_property_float2(
    float2 property_value = DEFAULT_PROPERTY_VALUE_FLOAT2
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup float2"),
    anno::description("Returns the property value. Returns float2(0.0) if the property does not exist or is an incompatible type. Must be a 2-component floating point or normalized integer type. 64-bit floating point values are converted to 32-bit floating point values."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export float3 cesium_property_float3(
    float3 property_value = DEFAULT_PROPERTY_VALUE_FLOAT3
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup float3"),
    anno::description("Returns the property value. Returns float3(0.0) if the property does not exist or is an incompatible type. Must be a 3-component floating point or normalized integer type. 64-bit floating point values are converted to 32-bit floating point values."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export float4 cesium_property_float4(
    float4 property_value = DEFAULT_PROPERTY_VALUE_FLOAT4
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup float4"),
    anno::description("Returns the property value. Returns float4(0.0) if the property does not exist or is an incompatible type. Must be a 4-component floating point or normalized integer type. 64-bit floating point values are converted to 32-bit floating point values."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export float2x2 cesium_property_float2x2(
    float2x2 property_value = DEFAULT_PROPERTY_VALUE_FLOAT2x2
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup float2x2"),
    anno::description("Returns the property value. Returns float2x2(0.0) if the property does not exist or is an incompatible type. Must be a MAT2 floating point or normalized integer type. 64-bit floating point values are converted to 32-bit floating point values."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export float3x3 cesium_property_float3x3(
    float3x3 property_value = DEFAULT_PROPERTY_VALUE_FLOAT3x3
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup float3x3"),
    anno::description("Returns the property value. Returns float3x3(0.0) if the property does not exist or is an incompatible type. Must be a MAT3 floating point or normalized integer type. 64-bit floating point values are converted to 32-bit floating point values."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export float4x4 cesium_property_float4x4(
    float4x4 property_value = DEFAULT_PROPERTY_VALUE_FLOAT4x4
    [[
        anno::hidden(),
        annotation_not_connectable()
    ]],
    uniform string property_id = string("")
    [[
        anno::unused()
    ]]
)
[[
    anno::display_name("Cesium property lookup float4x4"),
    anno::description("Returns the property value. Returns float4x4(0.0) if the property does not exist or is an incompatible type. Must be a MAT4 floating point or normalized integer type. 64-bit floating point values are converted to 32-bit floating point values."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]]
{
    return property_value;
}

export material cesium_material(
    color base_color_factor = color(1.0)
    [[
        anno::display_name("Base Color Factor"),
        anno::description("The base color of the material.")
    ]],
    float metallic_factor = 0.0
    [[
        anno::hard_range(0.0, 1.0),
        anno::display_name("Metallic Factor"),
        anno::description("The metalness of the material. Select between dielectric (0.0) and metallic (1.0).")
    ]],

    float roughness_factor = 1.0
    [[
        anno::hard_range(0.0, 1.0),
        anno::display_name("Roughness Factor"),
        anno::description("The roughness of the material. Select between very glossy (0.0) and dull (1.0).")
    ]],
    color emissive_factor = color(0.0)
    [[
        anno::display_name("Emissive Factor"),
        anno::description("The emissive color of the material.")
    ]],
    uniform gltf_alpha_mode alpha_mode = opaque
    [[
        anno::display_name("Alpha Mode"),
        anno::description("Select how to interpret the alpha value.")
    ]],
    float base_alpha = 1.0
    [[
        anno::hard_range(0.0, 1.0),
        anno::display_name("Base Alpha"),
        anno::description("Select between transparent (0.0) and opaque (1.0)."),
        anno::enable_if("alpha_mode!=opaque")
    ]],
    uniform float alpha_cutoff  = 0.5
    [[
        anno::hard_range(0.0, 1.0),
        anno::display_name("Alpha Cutoff"),
        anno::description("Threshold to decide between fully transparent and fully opaque when alpha mode is 'mask'."),
        anno::enable_if("alpha_mode==mask")
    ]]
) [[
    anno::display_name("Cesium PBR material"),
    anno::description("Cesium metallic-roughness material based off glTF PBR model"),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium")
]] = let {
    auto base_color = float3(base_color_factor);
    auto emissive = float3(emissive_factor);

    // We can't pass varyings (things not marked "uniform") to uniforms like base_color_factor, base_alpha, etc
    // To work around this, treat them as if they were texture values, which are varying.
    // If you look at gltf/pbr.mdl the math works out the same in either case.
    //
    // Previously our inputs were marked "uniform" but this caused a bunch of warnings like:
    //   uniform parameter 'base_color_factor' of material got varying attachment
    material base = gltf_material(
        base_color_texture: gltf_texture_lookup_value(true, float4(base_color.x, base_color.y, base_color.z, base_alpha)),
        metallic_roughness_texture: gltf_texture_lookup_value(true, float4(1.0, roughness_factor, metallic_factor, 1.0)),
        emissive_texture: gltf_texture_lookup_value(true, float4(emissive.x, emissive.y, emissive.z, 1.0)),
        alpha_mode: alpha_mode,
        alpha_cutoff: alpha_cutoff
    );

} in material(
    thin_walled: base.thin_walled,
    surface: base.surface,
    volume: base.volume,
    ior: base.ior,
    geometry: base.geometry
);

float4 alpha_blend(float4 src, float4 dst) {
    return src * float4(src.w, src.w, src.w, 1.0) + dst * (1.0 - src.w);
}

float4 compute_base_color(
    gltf_texture_lookup_value imagery_layer,
    float4 tile_color,
    gltf_texture_lookup_value base_color_texture,
    color base_color_factor,
    float base_alpha) {

    auto base_color_factor_float3 = float3(base_color_factor);

    auto base_color = base_color_texture.valid ? base_color_texture.value : float4(1.0);
    base_color *= float4(base_color_factor_float3.x, base_color_factor_float3.y, base_color_factor_float3.z, base_alpha);
    base_color *= ::scene::data_lookup_float4("COLOR_0", float4(1.0));
    base_color = alpha_blend(imagery_layer.value, base_color);
    base_color *= tile_color;

    return base_color;
}

// Copied from gltf/pbr.mdl since it's not exported
float2 khr_texture_transform_apply(
    float2 coord,
    float2 offset,
    float rotation,
    float2 scale
)
{
    // MDL expects the texture coordinate origin at the bottom left (gltf at top left)
    // Assuming the renderer follows the MDL specification in which case the coordinates
    // have been flipped either while loading the glTF geometry or while setting up the state.

    // Undo the flipping for the transformation to get into the original glTF texture space.
    coord = float2(coord.x, 1.0f - coord.y);

    // first scale
    coord = coord * scale;
    // then rotate
    float cos_rotation = ::math::cos(rotation);
    float sin_rotation = ::math::sin(rotation);
    coord = float2(cos_rotation * coord.x + sin_rotation * coord.y, cos_rotation * coord.y - sin_rotation * coord.x);
    // then translate
    coord = coord + offset;

    // flip back
    coord = float2(coord.x, 1.0f - coord.y);
    return coord;
}

struct texel_fetch_value
{
    bool valid = false;
    int4 value = int4(0, 0, 0, 0);
};

int mod(int a, int n) {
    return (a % n + n) % n;
}

int mirror(int a) {
    return a >= 0 ? a : -(1 + a);
}

int apply_mirrored_repeat(int x, int size) {
    return (size - 1) - mirror(mod(x, 2 * size) - size);
}

int apply_clamp_to_edge(int x, int size) {
    return math::clamp(x, 0, size - 1);
}

int apply_repeat(int x, int size) {
    return mod(x, size);
}

// Modified version of gltf_texture_lookup that uses texel_float4 instead of lookup_float4 to avoid
// linearly interpolating the texture values which would be incorrect for feature ID tetures. There
// doesn't seem to be an easier way to do nearest sampling. Unfortunately texel_float4 doesn't do
// texcoord wrapping so we need to do it ourselves.
texel_fetch_value texel_fetch(
    uniform texture_2d texture = texture_2d(),
    uniform int tex_coord_index = 0,
    uniform float2 offset = float2(0.0f, 0.0),
    uniform float rotation = 0.0,
    uniform float2 scale = float2(1.0f, 1.0f),
    uniform gltf_wrapping_mode wrap_s = repeat,
    uniform gltf_wrapping_mode wrap_t = repeat
)
{
    texel_fetch_value tex_ret;
    if (!tex::texture_isvalid(texture)) {
        return tex_ret;
    }

    auto tex_coord3 = state::texture_coordinate(tex_coord_index);
    auto tex_coord = khr_texture_transform_apply(
        coord: float2(tex_coord3.x, tex_coord3.y),
        offset: offset,
        rotation: rotation,
        scale: scale);

    auto width = tex::width(texture);
    auto height = tex::height(texture);

    auto pixel_x = int(tex_coord.x * width);
    auto pixel_y = int(tex_coord.y * height);

    switch (wrap_s) {
        case clamp_to_edge:
            pixel_x = apply_clamp_to_edge(pixel_x, width);
            break;
        case mirrored_repeat:
            pixel_x = apply_mirrored_repeat(pixel_x, width);
            break;
        case repeat:
            pixel_x = apply_repeat(pixel_x, width);
            break;
    }

    switch (wrap_t) {
        case clamp_to_edge:
            pixel_y = apply_clamp_to_edge(pixel_y, height);
            break;
        case mirrored_repeat:
            pixel_y = apply_mirrored_repeat(pixel_y, height);
            break;
        case repeat:
            pixel_y = apply_repeat(pixel_y, height);
            break;
    }

    auto value = tex::texel_float4(
        tex: texture,
        coord: int2(pixel_x, pixel_y));

    tex_ret.value = int4(math::round(value * 255.0));
    tex_ret.valid = true;
    return tex_ret;
}

export int cesium_internal_feature_id_texture_lookup(
    uniform int4 channels = int4(0),
    uniform int channel_count = 1,
    uniform int null_feature_id = DEFAULT_NULL_FEATURE_ID,
    // gltf_texture_lookup inputs below
    uniform texture_2d texture = texture_2d(),
    uniform int tex_coord_index = 0,
    uniform float2 offset = float2(0.0, 0.0),
    uniform float rotation = 0.0,
    uniform float2 scale = float2(1.0, 1.0),
    uniform gltf_wrapping_mode wrap_s = repeat,
    uniform gltf_wrapping_mode wrap_t = repeat
) [[ anno::hidden() ]] {
    auto texel_value = texel_fetch(
        texture: texture,
        tex_coord_index: tex_coord_index,
        offset: offset,
        rotation: rotation,
        scale: scale,
        wrap_s: wrap_s,
        wrap_t: wrap_t
    );

    if (!texel_value.valid) {
        return DEFAULT_NULL_FEATURE_ID;
    }

    // Feature IDs can be in multiple channels of the texture to form a single uint32
    auto feature_id = 0;
    for (int i = 0; i < channel_count; i++) {
        feature_id |= (texel_value.value[channels[i]] << (i * 8));
    }

    return feature_id == null_feature_id ? DEFAULT_NULL_FEATURE_ID : feature_id;
}

export int cesium_internal_feature_id_attribute_lookup(
    uniform string primvar_name,
    uniform int null_feature_id = DEFAULT_NULL_FEATURE_ID
) [[ anno::hidden() ]] {
    // Even if all feature ids in a triangle are the same value, the primvar interpolation math can yield non integer
    // results (e.g. 0.99999999 or 1.00000001), which when accessed as ints (i.e. floored) can cause variance across
    // the triangle surface. The fix is to read the primvar as a float, round the value, and cast to int.
    auto feature_id = int(::math::round(::scene::data_lookup_float(primvar_name)));
    return feature_id == null_feature_id ? DEFAULT_NULL_FEATURE_ID : feature_id;
}

float normalize_int(int value, int maximum_value) {
    return max(float(value) / float(maximum_value), -1.0);
}

float2 normalize_int2(int2 value, int maximum_value) {
    return max(float2(value) / float2(maximum_value), float2(-1.0));
}

float3 normalize_int3(int3 value, int maximum_value) {
    return max(float3(value) / float3(maximum_value), float3(-1.0));
}

float4 normalize_int4(int4 value, int maximum_value) {
    return max(float4(value) / float4(maximum_value), float4(-1.0));
}

export int cesium_internal_property_attribute_int_lookup(
    uniform string primvar_name,
    uniform bool has_no_data,
    uniform int no_data,
    uniform int default_value
) [[ anno::hidden() ]] {
    // See comment in DataType.h for why these are data_lookup_float instead of data_lookup_int
    const auto raw_value = int(::math::round(::scene::data_lookup_float(primvar_name)));
    const auto is_no_data = has_no_data && raw_value == no_data;
    return is_no_data ? default_value : raw_value;
}

export int2 cesium_internal_property_attribute_int2_lookup(
    uniform string primvar_name,
    uniform bool has_no_data,
    uniform int2 no_data,
    uniform int2 default_value
) [[ anno::hidden() ]] {
    const auto raw_value = int2(::math::round(::scene::data_lookup_float2(primvar_name)));
    const auto is_no_data = has_no_data && raw_value == no_data;
    return is_no_data ? default_value : raw_value;
}

export int3 cesium_internal_property_attribute_int3_lookup(
    uniform string primvar_name,
    uniform bool has_no_data,
    uniform int3 no_data,
    uniform int3 default_value
) [[ anno::hidden() ]] {
    const auto raw_value = int3(::math::round(::scene::data_lookup_float3(primvar_name)));
    const auto is_no_data = has_no_data && raw_value == no_data;
    return is_no_data ? default_value : raw_value;
}

export int4 cesium_internal_property_attribute_int4_lookup(
    uniform string primvar_name,
    uniform bool has_no_data,
    uniform int4 no_data,
    uniform int4 default_value
) [[ anno::hidden() ]] {
    const auto raw_value = int4(::math::round(::scene::data_lookup_float4(primvar_name)));
    const auto is_no_data = has_no_data && raw_value == no_data;
    return is_no_data ? default_value : raw_value;
}

export float cesium_internal_property_attribute_normalized_int_lookup(
    uniform string primvar_name,
    uniform float offset,
    uniform float scale,
    uniform int maximum_value,
    uniform bool has_no_data,
    uniform int no_data,
    uniform float default_value
) [[ anno::hidden() ]] {
    const auto raw_value = int(::math::round(::scene::data_lookup_float(primvar_name)));
    const auto is_no_data = has_no_data && raw_value == no_data;
    if (is_no_data) {
        return default_value;
    }
    const auto normalized_value = normalize_int(raw_value, maximum_value);
    const auto transformed_value = normalized_value * scale + offset;
    return transformed_value;
}

export float2 cesium_internal_property_attribute_normalized_int2_lookup(
    uniform string primvar_name,
    uniform float2 offset,
    uniform float2 scale,
    uniform int maximum_value,
    uniform bool has_no_data,
    uniform int2 no_data,
    uniform float2 default_value
) [[ anno::hidden() ]] {
    const auto raw_value = int2(::math::round(::scene::data_lookup_float2(primvar_name)));
    const auto is_no_data = has_no_data && raw_value == no_data;
    if (is_no_data) {
        return default_value;
    }
    const auto normalized_value = normalize_int2(raw_value, maximum_value);
    const auto transformed_value = normalized_value * scale + offset;
    return transformed_value;
}

export float3 cesium_internal_property_attribute_normalized_int3_lookup(
    uniform string primvar_name,
    uniform float3 offset,
    uniform float3 scale,
    uniform int maximum_value,
    uniform bool has_no_data,
    uniform int3 no_data,
    uniform float3 default_value
) [[ anno::hidden() ]] {
    const auto raw_value = int3(::math::round(::scene::data_lookup_float3(primvar_name)));
    const auto is_no_data = has_no_data && raw_value == no_data;
    if (is_no_data) {
        return default_value;
    }
    const auto normalized_value = normalize_int3(raw_value, maximum_value);
    const auto transformed_value = normalized_value * scale + offset;
    return transformed_value;
}

export float4 cesium_internal_property_attribute_normalized_int4_lookup(
    uniform string primvar_name,
    uniform float4 offset,
    uniform float4 scale,
    uniform int maximum_value,
    uniform bool has_no_data,
    uniform int4 no_data,
    uniform float4 default_value
) [[ anno::hidden() ]] {
    const auto raw_value = int4(::math::round(::scene::data_lookup_float4(primvar_name)));
    const auto is_no_data = has_no_data && raw_value == no_data;
    if (is_no_data) {
        return default_value;
    }
    const auto normalized_value = normalize_int4(raw_value, maximum_value);
    const auto transformed_value = normalized_value * scale + offset;
    return transformed_value;
}

export float cesium_internal_property_attribute_float_lookup(
    uniform string primvar_name,
    uniform float offset,
    uniform float scale,
    uniform bool has_no_data,
    uniform float no_data,
    uniform float default_value
) [[ anno::hidden() ]] {
    const auto raw_value = ::scene::data_lookup_float(primvar_name);
    const auto is_no_data = has_no_data && raw_value == no_data;
    if (is_no_data) {
        return default_value;
    }
    const auto transformed_value = raw_value * scale + offset;
    return transformed_value;
}

export float2 cesium_internal_property_attribute_float2_lookup(
    uniform string primvar_name,
    uniform float2 offset,
    uniform float2 scale,
    uniform bool has_no_data,
    uniform float2 no_data,
    uniform float2 default_value
) [[ anno::hidden() ]] {
    const auto raw_value = ::scene::data_lookup_float2(primvar_name);
    const auto is_no_data = has_no_data && raw_value == no_data;
    if (is_no_data) {
        return default_value;
    }
    const auto transformed_value = raw_value * scale + offset;
    return transformed_value;
}

export float3 cesium_internal_property_attribute_float3_lookup(
    uniform string primvar_name,
    uniform float3 offset,
    uniform float3 scale,
    uniform bool has_no_data,
    uniform float3 no_data,
    uniform float3 default_value
) [[ anno::hidden() ]] {
    const auto raw_value = ::scene::data_lookup_float3(primvar_name);
    const auto is_no_data = has_no_data && raw_value == no_data;
    if (is_no_data) {
        return default_value;
    }
    const auto transformed_value = raw_value * scale + offset;
    return transformed_value;
}

export float4 cesium_internal_property_attribute_float4_lookup(
    uniform string primvar_name,
    uniform float4 offset,
    uniform float4 scale,
    uniform bool has_no_data,
    uniform float4 no_data,
    uniform float4 default_value
) [[ anno::hidden() ]] {
    const auto raw_value = ::scene::data_lookup_float4(primvar_name);
    const auto is_no_data = has_no_data && raw_value == no_data;
    if (is_no_data) {
        return default_value;
    }
    const auto transformed_value = raw_value * scale + offset;
    return transformed_value;
}

export gltf_texture_lookup_value cesium_internal_texture_lookup(*) [[ anno::hidden() ]] = gltf_texture_lookup();

export gltf_texture_lookup_value cesium_internal_imagery_layer_lookup(
    uniform float alpha = 1.0,
    // gltf_texture_lookup inputs below
    uniform texture_2d texture = texture_2d(),
    uniform int tex_coord_index = 0,
    uniform float2 offset = float2(0.0, 0.0),
    uniform float rotation = 0.0,
    uniform float2 scale = float2(1.0, 1.0),
    uniform gltf_wrapping_mode wrap_s = repeat,
    uniform gltf_wrapping_mode wrap_t = repeat
) [[ anno::hidden() ]] {
    auto imagery_layer = gltf_texture_lookup(
        texture: texture,
        tex_coord_index: tex_coord_index,
        offset: offset,
        rotation: rotation,
        scale: scale,
        wrap_s: wrap_s,
        wrap_t: wrap_t
    );

    if (imagery_layer.valid) {
        imagery_layer.value.w *= alpha;
    }

    return imagery_layer;
}

export material cesium_internal_material(
    gltf_texture_lookup_value imagery_layer = gltf_texture_lookup_value(true, float4(0.0)),
    uniform float4 tile_color = float4(1.0),
    // gltf_material inputs below
    gltf_texture_lookup_value base_color_texture = gltf_texture_lookup_value(),
    uniform color base_color_factor = color(1.0),
    uniform float metallic_factor = 1.0,
    uniform float roughness_factor = 1.0,
    uniform color emissive_factor = color(0.0),
    uniform gltf_alpha_mode alpha_mode = opaque,
    uniform float base_alpha = 1.0,
    uniform float alpha_cutoff  = 0.5
) [[ anno::hidden() ]] = let {
    auto base_color = compute_base_color(imagery_layer, tile_color, base_color_texture, base_color_factor, base_alpha);
    material base = gltf_material(
        base_color_texture: gltf_texture_lookup_value(true, base_color),
        metallic_factor: metallic_factor,
        roughness_factor: roughness_factor,
        emissive_factor: emissive_factor,
        alpha_mode: alpha_mode,
        alpha_cutoff: alpha_cutoff
    );

} in material(
    thin_walled: base.thin_walled,
    surface: base.surface,
    volume: base.volume,
    ior: base.ior,
    geometry: base.geometry
);

export gltf_texture_lookup_value cesium_internal_imagery_layer_resolver(
    uniform int imagery_layers_count = 0,
    gltf_texture_lookup_value imagery_layer_0 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_1 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_2 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_3 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_4 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_5 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_6 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_7 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_8 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_9 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_10 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_11 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_12 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_13 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_14 = gltf_texture_lookup_value(),
    gltf_texture_lookup_value imagery_layer_15 = gltf_texture_lookup_value()
) [[ anno::hidden() ]] {
    gltf_texture_lookup_value[MAX_IMAGERY_LAYERS_COUNT] imagery_layers(
        imagery_layer_0,
        imagery_layer_1,
        imagery_layer_2,
        imagery_layer_3,
        imagery_layer_4,
        imagery_layer_5,
        imagery_layer_6,
        imagery_layer_7,
        imagery_layer_8,
        imagery_layer_9,
        imagery_layer_10,
        imagery_layer_11,
        imagery_layer_12,
        imagery_layer_13,
        imagery_layer_14,
        imagery_layer_15,
    );

    auto resolved_value = float4(0.0);

    for (int i = 0; i < imagery_layers_count; i++) {
        auto imagery_layer = imagery_layers[i];
        if (imagery_layer.valid) {
            resolved_value = alpha_blend(imagery_layer.value, resolved_value);
        }
    }

    return gltf_texture_lookup_value(true, resolved_value);
}
