mdl 1.8;

import ::anno::*;
import ::state::*;
import ::tex::*;

using ::gltf::pbr import *;

module [[
    anno::version( 1, 0, 0),
    anno::display_name("Cesium MDL functions")
]];

export enum up_axis_mode {
    Y,
    Z
};

float2 world_coordinate_2d(float2 min_world, float2 max_world, up_axis_mode up_axis)
{
    // Get the world pos of the pixel
    float3 world_pos = state::transform_vector(state::coordinate_internal, state::coordinate_world, state::position());
    float2 world_pos_horizontal = float2(world_pos.x, up_axis == Y ? world_pos.z : world_pos.y);

    // Return 0-1 UVs based on the min/max world coordinates provided
    return (world_pos_horizontal - min_world) / (max_world - min_world);
}

export float4 cesium_lookup_world_texture_float4(uniform texture_2d texture = texture_2d(), float2 min_world = float2(-5000.0, -5000.0), float2 max_world = float2(5000.0, 5000.0), up_axis_mode up_axis = Y)
[[
    anno::display_name("World-mapped texture lookup float4"),
    anno::description("Returns float4 from a texture mapped to world UV coordinates"),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium functions"),
    anno::ui_order(300)
]]
{
    return tex::lookup_float4(
        tex: texture,
        coord: world_coordinate_2d(min_world, max_world, up_axis),
        wrap_u: ::tex::wrap_clamp,
        wrap_v: ::tex::wrap_clamp);
}

export float3 cesium_lookup_world_texture_float3(uniform texture_2d texture = texture_2d(), float2 min_world = float2(-5000.0, -5000.0), float2 max_world = float2(5000.0, 5000.0), up_axis_mode up_axis = Y)
[[
    anno::display_name("World-mapped texture lookup float3"),
    anno::description("Returns float3 from a texture mapped to world UV coordinates"),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium functions"),
    anno::ui_order(300)
]]
{
    return tex::lookup_float3(
        tex: texture,
        coord: world_coordinate_2d(min_world, max_world, up_axis),
        wrap_u: ::tex::wrap_clamp,
        wrap_v: ::tex::wrap_clamp);
}

export color cesium_lookup_world_texture_color(uniform texture_2d texture = texture_2d(), float2 min_world = float2(-5000.0, -5000.0), float2 max_world = float2(5000.0, 5000.0), up_axis_mode up_axis = Y)
[[
    anno::display_name("World-mapped texture lookup color"),
    anno::description("Returns color from a texture mapped to world UV coordinates"),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium functions"),
    anno::ui_order(300)
]]
{
    return tex::lookup_color(
        tex: texture,
        coord: world_coordinate_2d(min_world, max_world, up_axis),
        wrap_u: ::tex::wrap_clamp,
        wrap_v: ::tex::wrap_clamp);
}

export color cesium_base_color_texture(
    gltf_texture_lookup_value base_color_texture
    [[
        anno::hidden()
    ]]
)
[[
    anno::display_name("Base color texture lookup color"),
    anno::description("Returns the base color texture of the tile. Returns [0, 0, 0] if the base color texture does not exist."),
    anno::author("Cesium GS Inc."),
    anno::in_group("Cesium functions"),
    anno::ui_order(300)
]]
{
    return base_color_texture.valid ? color(base_color_texture.value.x, base_color_texture.value.y, base_color_texture.value.z) : color(0.0);
}

float4 alpha_blend(float4 src, float4 dst) {
    return src * float4(src.w, src.w, src.w, 1.0) + dst * (1.0 - src.w);
}

export gltf_texture_lookup_value cesium_texture_lookup(*) [[ anno::hidden() ]] = gltf_texture_lookup();

export material cesium_material(
    gltf_texture_lookup_value imagery_layers_texture = gltf_texture_lookup_value(true, float4(0.0)),
    uniform color debug_color = color(1.0),
    // gltf_material inputs below
    gltf_texture_lookup_value base_color_texture = gltf_texture_lookup_value(),
    uniform color base_color_factor = color(1.0),
    uniform float metallic_factor = 1.0,
    uniform float roughness_factor = 1.0,
    uniform color emissive_factor = color(0.0),
    uniform gltf_alpha_mode alpha_mode = opaque,
    uniform float base_alpha = 1.0,
    uniform float alpha_cutoff  = 0.5
) [[ anno::hidden() ]] = let {
    auto base_color_texture_value = base_color_texture.valid ? base_color_texture.value : float4(1.0);
    auto final_base_color_texture_value = alpha_blend(imagery_layers_texture.value, base_color_texture_value);
    auto final_base_color_texture = gltf_texture_lookup_value(true, final_base_color_texture_value);
    auto final_base_color_factor = base_color_factor * debug_color;

    material base = gltf_material(
        base_color_factor: final_base_color_factor,
        base_color_texture: final_base_color_texture,
        metallic_factor: metallic_factor,
        roughness_factor: roughness_factor,
        emissive_factor: emissive_factor,
        alpha_mode: alpha_mode,
        base_alpha: base_alpha,
        alpha_cutoff: alpha_cutoff
    );

} in material(
    thin_walled: base.thin_walled,
    surface: base.surface,
    volume: base.volume,
    ior: base.ior,
    geometry: base.geometry
);

export gltf_texture_lookup_value cesium_imagery_layer_resolver(
    uniform int imagery_layers_count = 0,
    gltf_texture_lookup_value imagery_layer_0 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_1 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_2 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_3 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_4 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_5 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_6 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_7 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_8 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_9 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_10 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_11 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_12 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_13 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_14 = gltf_texture_lookup(),
    gltf_texture_lookup_value imagery_layer_15 = gltf_texture_lookup()
) [[ anno::hidden() ]] {
    // The array length should match MAX_IMAGERY_LAYERS_COUNT in Tokens.h
    gltf_texture_lookup_value[16] imagery_layers(
        imagery_layer_0,
        imagery_layer_1,
        imagery_layer_2,
        imagery_layer_3,
        imagery_layer_4,
        imagery_layer_5,
        imagery_layer_6,
        imagery_layer_7,
        imagery_layer_8,
        imagery_layer_9,
        imagery_layer_10,
        imagery_layer_11,
        imagery_layer_12,
        imagery_layer_13,
        imagery_layer_14,
        imagery_layer_15,
    );

    auto resolved_value = float4(0.0);

    for (int i = 0; i < imagery_layers_count; i++) {
        auto imagery_layer = imagery_layers[i];
        if (imagery_layer.valid) {
            resolved_value = alpha_blend(imagery_layer.value, resolved_value);
        }
    }

    return gltf_texture_lookup_value(true, resolved_value);
}
